import "animation.rol";
import "levelgen.rol";
import "sprites.rol";

static GAME_OVER: bool;
static PLAYER_POSITION: Position;
const PLAYER_WIDTH: f64 = 16.0;
const PLAYER_HEIGHT: f64 = 16.0;
static PLAYER_FACING_LEFT: bool;
static PLAYER_MOVED_ON_LAST_FRAME: bool;
const TILE_WIDTH: f64 = 16.0;
const TILE_HEIGHT: f64 = 16.0;
static JUMP_SPEED: f64;
const TERMINAL_VELOCITY: f64 = 3.5;
static JUMP_STATE: JumpState;
static RNG: u64 = 0;
static SCORE_BUF: [u8; 10];
static SCORE: u32;

static DOING_DOOR: bool = false;

// Timers
static TIME_ALIVE: u64;
static TIME_DEAD: u64;
static TIME_DOING_DOOR: u64;

static LAST_GAMEPAD: u8;

static TILES: [Tile; 1600];

struct Position {
   x: f64,
   y: f64,
}

enum Tile {
   Wall,
   Spike,
   Air,
   Coin,
   Door,
}

enum JumpState {
   Standing,
   Jumping,
   Falling,
}

proc dist(position_1: Position, position_2: Position) -> f64 {
   let x_diff = position_1.x - position_2.x;
   let y_diff = position_1.y - position_2.y;
   return sqrt((x_diff * x_diff) + (y_diff * y_diff));
}

proc rand() -> u64 {
   RNG = RNG ^ (RNG << 13);
   RNG = RNG ^ (RNG >> 7);
   RNG = RNG ^ (RNG << 17);
   return RNG;
}

proc rand_range(rand_min: u64, rand_max: u64) -> u64 {
   return rand() % (rand_max + 1 - rand_min) + rand_min;
}

proc one_in(x: u64) -> bool {
   return rand_range(0, x - 1) == 0;
}

proc rectangles_collide(x1: f64, y1: f64, width1: f64, height1: f64, x2: f64, y2: f64, width2: f64, height2: f64) -> bool {
   return
      (x1 < x2 + width2) &
      (x1 + width1 > x2) &
      (y1 < y2 + height2) &
      (height1 + y1 > y2);
}

proc segment_v_segment(x1: f64, y1: f64, x2: f64, y2: f64, x3: f64, y3: f64, x4: f64, y4: f64) -> bool {
   let dx1 = x2 - x1;
   let dy1 = y2 - y1;
   let dx2 = x4 - x3;
   let dy2 = y4 - y3;
   let dx3 = x1 - x3;
   let dy3 = y1 - y3;
   let d = dx1*dy2 - dy1*dx2;
   if d == 0.0 {
      return false;
   }
   let t1 = (dx2*dy3 - dy2*dx3)/d;
   if t1 < 0.0 or t1 > 1.0 {
      return false;
   }
   let t2 = (dx1*dy3 - dy1*dx3)/d;
   if t2 < 0.0 or t2 > 1.0 {
    return false;
   }
   return true;
}

proc reset_game(starting_score: u32) {
   GAME_OVER = false;
   TIME_DEAD = 0;
   JUMP_SPEED = 0.0;
   JUMP_STATE = JumpState::Standing;
   GHOST_IS_PRESENT = false;
   TIME_ALIVE = 0;
   SCORE = starting_score;
   DOOR_CYCLE.current_time_in_animation_seconds = 0.0;

   if RNG == 0 {
      diskr((&RNG) transmute &u8, 8);
      if RNG == 0 {
         RNG = 2;
      }
   }
   diskw((&RNG) transmute &u8, 8);

   carve_path();

   // ensure that the player can't yeet out of the map

   // top row
   let max: usize = 40;
   for i in 0..max {
      TILES[i] = Tile::Wall;
   }

   // bottom row
   for i in 1560..TILES.length {
      TILES[i] = Tile::Wall;
   }

   // left column
   let i: usize = 0;
   loop {
      TILES[i] = Tile::Wall;
      i = i + 40;
      if i >= 1600 {
         break;
      }
   }

   i = 39;
   loop {
      TILES[i] = Tile::Wall;
      i = i + 40;
      if i >= 1600 {
         break;
      }
   }
}

proc shade(rgb: u32, shade_factor: f64) -> u32 {
   let b = ((rgb & 0x0000FF) truncate f64) / 255.0;
   let g = (((rgb & 0x00FF00) >> 8) truncate f64) / 255.0;
   let r = (((rgb & 0xFF0000) >> 16) truncate f64) / 255.0;

   let new_r = ((r * (1.0 - shade_factor)) * 255.0) truncate u32;
   let new_g = ((g * (1.0 - shade_factor)) * 255.0) truncate u32;
   let new_b = ((b * (1.0 - shade_factor)) * 255.0) truncate u32;

   return (new_r << 16) | (new_b << 8) | new_g;
}

proc start() {
   //         [ black,   grey,    brown,     white  ]
   PALETTE~ = [0x211e20, 0x555568,0xa0a08b,0xe9efec];
   DRAW_COLORS~ = 0x2340;
   make_maps();
   reset_game(0);
}

proc sim(dt: f64) {
   if GAME_OVER | DOING_DOOR {
      return;
   }

   {
      if (!GHOST_IS_PRESENT) {
         maybe_spawn_ghost();
      }
      move_ghost(dt);
   }

   // Handle input
   let jump_pressed = (GAMEPADS~[0] & BUTTON_1) > 0;
   if jump_pressed and JUMP_STATE == JumpState::Standing {
      JUMP_STATE = JumpState::Jumping;
      JUMP_SPEED = 9.0;
      tone(300 | (800 << 16), 6, 50, TONE_MODE1);
   }

   let left_pressed = (GAMEPADS~[0] & BUTTON_LEFT) > 0;
   let right_pressed = (GAMEPADS~[0] & BUTTON_RIGHT) > 0;

   let go_left = left_pressed & !right_pressed;
   let go_right = right_pressed & !left_pressed;

   let offset: f64 = 0.0;
   if go_left {
      PLAYER_POSITION.x = PLAYER_POSITION.x - (2.0 * dt);
      offset = TILE_WIDTH;
      PLAYER_FACING_LEFT = true;
      PLAYER_MOVED_ON_LAST_FRAME = true;
   } else if go_right {
      PLAYER_POSITION.x = PLAYER_POSITION.x + (2.0 * dt);
      offset = TILE_WIDTH*-1.0;
      PLAYER_FACING_LEFT = false;
      PLAYER_MOVED_ON_LAST_FRAME = true;
   }
   else {
      PLAYER_MOVED_ON_LAST_FRAME = false;
   }

   for i in 0..TILES.length {
      if TILES[i] == Tile::Air {
         continue;
      }

      let x = ((i % 40) * 16) truncate f64;
      let y = ((i / 40) * 16) truncate f64;
      if rectangles_collide(PLAYER_POSITION.x, PLAYER_POSITION.y, PLAYER_WIDTH, PLAYER_HEIGHT, x, y, TILE_WIDTH, TILE_HEIGHT) {
         if TILES[i] == Tile::Coin {
            handle_coin_collision(i);
         } else if TILES[i] == Tile::Door {
            PLAYER_POSITION.x = x + offset;
            handle_door_collision();
         } else if TILES[i] == Tile::Spike {
                  let midpoint = x + (TILE_WIDTH / 2.0);
                  // left edge of player
                  let fipt = segment_v_segment(PLAYER_POSITION.x, PLAYER_POSITION.y, PLAYER_POSITION.x, PLAYER_POSITION.y + PLAYER_HEIGHT, midpoint, y, x, y + TILE_HEIGHT);
                  let spt = segment_v_segment(PLAYER_POSITION.x, PLAYER_POSITION.y, PLAYER_POSITION.x, PLAYER_POSITION.y + PLAYER_HEIGHT, midpoint, y, x + TILE_WIDTH, y + TILE_HEIGHT);
                  // right edge of player
                  let tpt = segment_v_segment(PLAYER_POSITION.x + PLAYER_WIDTH, PLAYER_POSITION.y, PLAYER_POSITION.x + PLAYER_WIDTH, PLAYER_POSITION.y + PLAYER_HEIGHT, midpoint, y, x, y + TILE_HEIGHT);
                  let fopt = segment_v_segment(PLAYER_POSITION.x + PLAYER_WIDTH, PLAYER_POSITION.y, PLAYER_POSITION.x + PLAYER_WIDTH, PLAYER_POSITION.y + PLAYER_HEIGHT, midpoint, y, x + TILE_WIDTH, y + TILE_HEIGHT);
                  // bottom of player
                  let ffpt = segment_v_segment(PLAYER_POSITION.x, PLAYER_POSITION.y + PLAYER_HEIGHT, PLAYER_POSITION.x + PLAYER_WIDTH, PLAYER_POSITION.y + PLAYER_HEIGHT, midpoint, y, x, y + TILE_HEIGHT);
                  let fifpt = segment_v_segment(PLAYER_POSITION.x, PLAYER_POSITION.y + PLAYER_HEIGHT, PLAYER_POSITION.x + PLAYER_WIDTH, PLAYER_POSITION.y + PLAYER_HEIGHT, midpoint, y, x + TILE_WIDTH, y + TILE_HEIGHT);
                  let touched_spike = fipt | spt | tpt | fopt | fipt | ffpt | fifpt;
                  if touched_spike {
                     PLAYER_POSITION.x = x + offset;
                  }
         } else {
            PLAYER_POSITION.x = x + offset;
         }
      }
   }
   if (GHOST_IS_PRESENT) {
      // Check if colliding with ghost
      if rectangles_collide(PLAYER_POSITION.x, PLAYER_POSITION.y, PLAYER_WIDTH, PLAYER_HEIGHT, GHOST_POSITION.x, GHOST_POSITION.y, GHOST_WIDTH truncate f64, GHOST_HEIGHT truncate f64) {
            maybe_play_game_over_noise();
            GAME_OVER = true;
            return;
      }
   }

   // Do jumping
   {
      if JUMP_STATE == JumpState::Jumping {
         JUMP_SPEED = JUMP_SPEED - (0.7 * dt);
         PLAYER_POSITION.y = PLAYER_POSITION.y - (JUMP_SPEED * dt);
         if (JUMP_SPEED <= 0.0) {
            JUMP_SPEED = 0.0;
            JUMP_STATE = JumpState::Falling;
         }

         for i in 0..TILES.length {
            if TILES[i] == Tile::Air {
               continue;
            }

            let x = ((i % 40) * 16) truncate f64;
            let y = ((i / 40) * 16) truncate f64;
            if rectangles_collide(PLAYER_POSITION.x, PLAYER_POSITION.y, 16.0, 16.0, x, y, 16.0, 16.0) {
               if TILES[i] == Tile::Coin {
                  handle_coin_collision(i);
               } else if TILES[i] == Tile::Spike {
                  // h,,,
               } else {
                  PLAYER_POSITION.y = y + 16.0;
                  JUMP_SPEED = 0.0;
                  JUMP_STATE = JumpState::Falling;
               }
            }
         }
      } else if JUMP_STATE == JumpState::Falling {
         JUMP_SPEED = JUMP_SPEED + (0.2 * dt);
         if (JUMP_SPEED > TERMINAL_VELOCITY) {
            JUMP_SPEED = TERMINAL_VELOCITY;
         }

         PLAYER_POSITION.y = PLAYER_POSITION.y + (JUMP_SPEED * dt);

         let touched_door: bool = false;
         let touched_spike: bool = false;
         for i in 0..TILES.length {
            if TILES[i] == Tile::Air{
               continue;
            }
            let x = ((i % 40) * 16) truncate f64;
            let y = ((i / 40) * 16) truncate f64;
            if rectangles_collide(PLAYER_POSITION.x, PLAYER_POSITION.y, PLAYER_WIDTH, PLAYER_HEIGHT, x, y, TILE_WIDTH, TILE_HEIGHT) {
               if TILES[i] == Tile::Coin {
                  handle_coin_collision(i);
               } else if TILES[i] == Tile::Spike {
                  let midpoint = x + (TILE_WIDTH / 2.0);
                  // left edge of player
                  let fipt = segment_v_segment(PLAYER_POSITION.x, PLAYER_POSITION.y, PLAYER_POSITION.x, PLAYER_POSITION.y + PLAYER_HEIGHT, midpoint, y, x, y + TILE_HEIGHT);
                  let spt = segment_v_segment(PLAYER_POSITION.x, PLAYER_POSITION.y, PLAYER_POSITION.x, PLAYER_POSITION.y + PLAYER_HEIGHT, midpoint, y, x + TILE_WIDTH, y + TILE_HEIGHT);
                  // right edge of player
                  let tpt = segment_v_segment(PLAYER_POSITION.x + PLAYER_WIDTH, PLAYER_POSITION.y, PLAYER_POSITION.x + PLAYER_WIDTH, PLAYER_POSITION.y + PLAYER_HEIGHT, midpoint, y, x, y + TILE_HEIGHT);
                  let fopt = segment_v_segment(PLAYER_POSITION.x + PLAYER_WIDTH, PLAYER_POSITION.y, PLAYER_POSITION.x + PLAYER_WIDTH, PLAYER_POSITION.y + PLAYER_HEIGHT, midpoint, y, x + TILE_WIDTH, y + TILE_HEIGHT);
                  // bottom of player
                  let ffpt = segment_v_segment(PLAYER_POSITION.x, PLAYER_POSITION.y + PLAYER_HEIGHT, PLAYER_POSITION.x + PLAYER_WIDTH, PLAYER_POSITION.y + PLAYER_HEIGHT, midpoint, y, x, y + TILE_HEIGHT);
                  let fifpt = segment_v_segment(PLAYER_POSITION.x, PLAYER_POSITION.y + PLAYER_HEIGHT, PLAYER_POSITION.x + PLAYER_WIDTH, PLAYER_POSITION.y + PLAYER_HEIGHT, midpoint, y, x + TILE_WIDTH, y + TILE_HEIGHT);
                  touched_spike = fipt | spt | tpt | fopt | fipt | ffpt | fifpt;
               } else if TILES[i] == Tile::Door {
                  touched_door = true;
                  PLAYER_POSITION.y = y - TILE_HEIGHT;
                  JUMP_STATE = JumpState::Standing;
                  break;
               } else {
                  PLAYER_POSITION.y = y - TILE_HEIGHT;
                  JUMP_STATE = JumpState::Standing;
               }
            }
         }
         if touched_door {
            handle_door_collision();
         } else if touched_spike {
            // hit a spike, wasn't touching door. end early.
            maybe_play_game_over_noise();
            GAME_OVER = true;
            return;
         }
      } else if JUMP_STATE == JumpState::Standing {
         let ontop = false;
         for i in 0..TILES.length {
            if TILES[i] == Tile::Air or TILES[i] == Tile::Coin or TILES[i] == Tile::Spike or TILES[i] == Tile::Door {
               continue;
            }

            let x = ((i % 40) * 16) truncate f64;
            let y = ((i / 40) * 16) truncate f64;
            if rectangles_collide(PLAYER_POSITION.x, PLAYER_POSITION.y + 0.1, PLAYER_WIDTH, PLAYER_HEIGHT, x, y, TILE_WIDTH, TILE_HEIGHT) {
               ontop = true;
               break;
            }
         }
         if !ontop {
            JUMP_SPEED = 0.0;
            JUMP_STATE = JumpState::Falling;
         }
      }
   }
}

proc handle_coin_collision(current_tile_index: usize) {
   tone(500 | (1000 << 16), 3, 50, TONE_MODE1);
   SCORE = SCORE + 1;
   TILES[current_tile_index] = Tile::Air;
}

proc handle_door_collision() {
   DOING_DOOR = true;
   tone(90 | (160 << 16), (100 << 24) | (86 << 16) | 10 | (166 << 8), 3, 3 | (0 << 2));
   SCORE = SCORE + 5;
}

proc update() {
   update_timers();

   let gamepad = GAMEPADS~[0];
   let pressed_this_frame = gamepad & (gamepad ^ LAST_GAMEPAD);
   LAST_GAMEPAD = gamepad;

   let reset_pressed = (pressed_this_frame & BUTTON_2) > 0;
   if reset_pressed {
      reset_game(0);
   }

   if DOING_DOOR {
      PALETTE~[0] = shade(PALETTE~[0], 0.01);
      PALETTE~[1] = shade(PALETTE~[1], 0.01);
      PALETTE~[2] = shade(PALETTE~[2], 0.01);
      PALETTE~[3] = shade(PALETTE~[3], 0.01);

      if TIME_DOING_DOOR > 150 {
         DOING_DOOR = false;
         TIME_DOING_DOOR = 0;
         PALETTE~ = [0x211e20, 0x555568,0xa0a08b,0xe9efec];
         reset_game(SCORE);
         return;
      }
   } else {
      sim(0.1);
      sim(0.1);
      sim(0.1);
      sim(0.1);
      sim(0.1);
      sim(0.1);
      sim(0.1);
      sim(0.1);
      sim(0.1);
      sim(0.1);
   }

   // Draw world
   {
      let camera_position = Position {
         x: PLAYER_POSITION.x + 5.0,
         y: PLAYER_POSITION.y + 8.0,
      };
      camera_position.x = PLAYER_POSITION.x + 5.0;
      camera_position.y = PLAYER_POSITION.y + 8.0;

      let camera_offset_x: f64 = (camera_position.x) - 80.0;
      let camera_offset_y: f64 = (camera_position.y) - 80.0;

      // Draw player
      draw_player((PLAYER_POSITION.x - camera_offset_x) truncate i32, (PLAYER_POSITION.y - camera_offset_y) truncate i32, JUMP_STATE);

      if (GHOST_IS_PRESENT) {
         draw_ghost(camera_offset_x, camera_offset_y);
      }

      for i in 0..TILES.length {
         let x = ((i % 40) * 16) truncate f64;
         let y = ((i / 40) * 16) truncate f64;
         if TILES[i] == Tile::Air {
            continue;
         } else if TILES[i] == Tile::Wall {
            blit_sub(&BLOCKS[0], (x - camera_offset_x) truncate i32, (y - camera_offset_y) truncate i32, 16, 16, 0, 0, BLOCKS_WIDTH, BLOCKS_FLAGS);
         } else if TILES[i] == Tile::Spike {
            blit_sub(&SPIKES[0], (x - camera_offset_x) truncate i32, (y - camera_offset_y) truncate i32, 16, 16, 0, 0, SPIKES_WIDTH, SPIKES_FLAGS);
         } else if TILES[i] == Tile::Door{
            if DOING_DOOR {
               let door_spritesheet_x = advance_animation_and_return_x(&DOOR_CYCLE, (1.0/60.0));
               blit_sub(&DOOR[0], (x - camera_offset_x) truncate i32, (y - camera_offset_y) truncate i32, 16, 16, door_spritesheet_x, 0, DOOR_WIDTH, DOOR_FLAGS);
            } else {
               blit_sub(&DOOR[0], (x - camera_offset_x) truncate i32, (y - camera_offset_y) truncate i32, 16, 16, 0, 0, DOOR_WIDTH, DOOR_FLAGS);
            }
         } else if TILES[i] == Tile::Coin {
            let coin_spritesheet_x = advance_animation_and_return_x(&COIN_CYCLE, (1.0/60.0));
            blit_sub(&COIN[0], (x - camera_offset_x) truncate i32, (y - camera_offset_y) truncate i32, 16, 16, coin_spritesheet_x, 0, COIN_WIDTH, COIN_FLAGS);
         }
      }
   }

   //trace(int_to_string(PLAYER_POSITION.y truncate u32, &SCORE_BUF));

   // Draw UI
   {
      let tmp: u16 = DRAW_COLORS~;
      // use black (color 1) as text color, and white (color 4) as the background.
      DRAW_COLORS~ = 0x2341;
      text("SCORE: ", 2, 2);
      text(int_to_string(SCORE, &SCORE_BUF), 47, 2);
      DRAW_COLORS~ = tmp;
      if GAME_OVER {
         draw_game_over_text();
      }
   }
}

const JUMP_SPRITE_X: i32 = 320;
const FALLING_SPRITE_X: i32 = 340;

proc draw_player(player_x: i32, player_y: i32, jump_state: JumpState) {
   let tmp: u16 = DRAW_COLORS~;
   DRAW_COLORS~ = 0x2340;

   let current_sprite_y: i32 = 0;
   let current_sprite_x: i32 = get_next_player_sprite_x(jump_state, PREVIOUS_JUMP_STATE);
   blit_sub(&PLAYER_SPRITESHEET[0], player_x -1 , player_y - 2, 20, 18, current_sprite_x, current_sprite_y, PLAYER_SPRITESHEET_WIDTH, PLAYER_SPREADSHEET_FLAGS | (PLAYER_FACING_LEFT extend u32 << 1));
   // one array for jumping (8 sprites), one array for running (2 sprites), one array for idle (8 sprites).
   DRAW_COLORS~ = tmp;
}

const GHOST_SPAWN_TIME: u64 = 60*25; // Number of frames after which the ghost should spawn.
static GHOST_IS_PRESENT: bool;
static GHOST_FACING_LEFT: bool;
static GHOST_POSITION: Position;
const GHOST_HEIGHT: u32 = 30;
const GHOST_WIDTH: u32 = 30;
const GHOST_SPEED: u64 = 1;
const DEFAULT_VOLUME: u32 = 50;


proc update_timers() {
   if GAME_OVER {
      TIME_DEAD = TIME_DEAD + 1;
   } else {
      TIME_ALIVE = TIME_ALIVE + 1;
      if DOING_DOOR {
         TIME_DOING_DOOR = TIME_DOING_DOOR + 1;
      }
   }
}

proc maybe_spawn_ghost() {
   if (TIME_ALIVE > GHOST_SPAWN_TIME) {
      GHOST_IS_PRESENT = true;
      // TODO: spawn in opposite corner of player, instead of top left.
      GHOST_POSITION.x = 0.0;
      GHOST_POSITION.y = 0.0;

      let starting_freq: u32 = 250;
      let ending_freq: u32 = 350;
      let decay: u32 = 42;
      let release: u32 = 106;
      tone(starting_freq | (ending_freq << 16), (0 | (release << 8) | (decay << 16)), DEFAULT_VOLUME, TONE_PULSE2 | TONE_MODE1);
   }
}

// Caller should check if GHOST_IS_PRESENT before moving.
proc move_ghost(dt: f64) {
   // if player x > ghost x, ghost x++
   // if player x < ghost x, ghost x--
   if (PLAYER_POSITION.x > GHOST_POSITION.x) {
      GHOST_POSITION.x = GHOST_POSITION.x + (1.0 * dt);
      GHOST_FACING_LEFT = false;
   }
   else {
      GHOST_POSITION.x = GHOST_POSITION.x - (1.0 * dt);
      GHOST_FACING_LEFT = true;
   }
   if (PLAYER_POSITION.y >= GHOST_POSITION.y) {
      GHOST_POSITION.y = GHOST_POSITION.y + (1.0 * dt);
   }
   else {
      GHOST_POSITION.y = GHOST_POSITION.y - (1.0 * dt);
   }
}

// TODO: add some other color to the ghost to make it stand out.
proc draw_ghost(camera_offset_x: f64, camera_offset_y: f64) {
   // Draw ghost body.
   let ghost_draw_x = (GHOST_POSITION.x - camera_offset_x) truncate i32;
   let ghost_draw_y = (GHOST_POSITION.y - camera_offset_y) truncate i32;
   rect(ghost_draw_x, ghost_draw_y, GHOST_WIDTH, GHOST_HEIGHT);
   // Draw ghost eyes.

   let ghost_left_eye_x = ghost_draw_x;
   let ghost_right_eye_x = ghost_draw_x;
   if GHOST_FACING_LEFT {
      ghost_left_eye_x =  ghost_left_eye_x + 5;
      ghost_right_eye_x =  ghost_right_eye_x + 10;
   } else {
      ghost_left_eye_x =  ghost_left_eye_x + 15;
      ghost_right_eye_x =  ghost_right_eye_x + 20;
   }
   rect(ghost_right_eye_x, ghost_draw_y + 6, 4, 5);
   rect(ghost_left_eye_x, ghost_draw_y + 4, 4, 5);
   // draw mouth
   rect(ghost_left_eye_x, ghost_draw_y + 12, 12, 10);

}

proc draw_game_over_text() {
   let tmp: u16 = DRAW_COLORS~;
   // use black (color 1) as text color, and white (color 4) as the background.
   DRAW_COLORS~ = 0x2341;
   text("DEATH", 40, 50);
   DRAW_COLORS~ = tmp;
}

proc maybe_play_game_over_noise() {
   if !GAME_OVER {
     let starting_freq: u32 = 590;
     let ending_freq: u32 = 110;
     tone(starting_freq| (ending_freq << 16), 60, DEFAULT_VOLUME, TONE_PULSE1 | TONE_MODE3);
   }
}
