import "wasm_shared.rol";
import "malloc.rol";
import "ryu/f2s.rol";
import "ryu/d2s.rol";

builtin proc wasm_memory_size() -> usize;
builtin proc wasm_memory_grow(pages: usize) -> usize;

struct ciovec {
   buf: &u8,
   buf_len: usize,
}

struct prestat {
   tag: u8,
   u: u32,
}

extern proc fd_write(handle: usize, strings_to_write: &ciovec, strings_to_write_len: usize, written_ptr: &usize) -> isize;
extern proc path_open(fd: usize, dirflags: u32, path: String, oflags: u16, fs_rights_base: u64, fs_rights_inheriting: u64, fdflags: u16, ret: &usize) -> isize;
extern proc fd_close(fd: usize) -> isize;
extern proc fd_prestat_get(fd: usize, result: &prestat) -> isize;

proc print(s: String) {
   let data = [
      ciovec {
         buf: s.pointer,
         buf_len: s.length,
      },
   ];

   let written: usize = 0;
   loop {
      fd_write(1, &data[0], data.length, &written);
      let remaining_len = &data[0].buf_len;
      if written >= remaining_len~ {
         break;
      } else {
         data[0].buf = (data[0].buf transmute usize + written) transmute &u8;
         remaining_len~ = remaining_len~ - written;
      }
   }
}

proc println(s: String) {
   let data = [
      ciovec {
         buf: s.pointer,
         buf_len: s.length,
      },
      ciovec {
         buf: "\n".pointer,
         buf_len: "\n".length,
      },
   ];

   let written: usize = 0;
   loop {
      fd_write(1, &data[0], data.length, &written);
      let remaining_len = &data[0].buf_len;
      if written > remaining_len~ {
         break;
      } else {
         data[0].buf = (data[0].buf transmute usize + written) transmute &u8;
         remaining_len~ = remaining_len~ - written;
      }
   }
}

proc println_bool(value: bool) {
   println(bool_to_string(value));
}

proc count_digits(value: u64) -> u8 {
   let digit_count: u8 = 0;

   loop {
      if value <= 9 {
         return digit_count + 1;
      }

      digit_count = digit_count + 1;
      value = value / 10;
   }
}

proc f32_to_string(f: f32) -> String {
   let result: &u8 = malloc(16, 1).buf;
   return ryu_f2s_buffered(f, result);
}

proc f64_to_string(f: f64) -> String {
   let result: &u8 = malloc(25, 1).buf;
   return ryu_d2s_buffered(f, result);
}

proc _wasi_int_to_string_inner(value: u64, buf: &u8) -> String {
   let digit_count = count_digits(value);
   let digit_count_for_loop = digit_count as usize - 1;

   let next_digit: u8 = 0;
   let i: usize = 0;

   loop {
      if value <= 9 {
         next_digit = value as u8;
         let ptr = ((buf transmute usize) + digit_count_for_loop - i) transmute &u8;
         ptr~ = next_digit + 48;
         i = i + 1;
         break;
      }
      next_digit = (value % 10) as u8;
      value = value / 10;
      let ptr = ((buf transmute usize) + digit_count_for_loop - i) transmute &u8;
      ptr~ = next_digit + 48;
      i = i + 1;
   }

   return String {
      length: i,
      pointer: buf,
   };
}

proc uint_to_string(value: u64) -> String {
   let buf = malloc(20, 1).buf;
   return _wasi_int_to_string_inner(value, buf);
}

proc int_to_string(value: i64) -> String {
   if value == I64_MIN {
      return "-9223372036854775808";
   } else if value < 0 {
      let buf = malloc(20, 1).buf;
      buf~ = "-".pointer~;
      let adjusted_buf = buf transmute usize;
      adjusted_buf = adjusted_buf + 1;
      let ustring = _wasi_int_to_string_inner(-value as u64, adjusted_buf transmute &u8);
      return String {
         length: ustring.length + 1,
         pointer: buf,
      };
   } else {
      return uint_to_string(value as u64);
   }
}

const _WASM_PAGE_SIZE: usize = 65535;

proc brk(new_break: usize) -> usize {
   // First page is reserved for stack, statics
   static BRK: usize = _WASM_PAGE_SIZE + 1;

   if new_break <= BRK {
      return BRK;
   }

   let needed_pages = new_break / _WASM_PAGE_SIZE + (new_break % _WASM_PAGE_SIZE != 0) as usize;
   if needed_pages > 65535 {
      return BRK;
   }

   wasm_memory_grow(needed_pages - wasm_memory_size());
   BRK = new_break;

   return BRK;
}