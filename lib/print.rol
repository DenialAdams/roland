struct String {
   pointer: &u8,
   length: usize,
}

proc print(s: String) {
   let data: [usize; 4] = [s.pointer transmute usize, s.length, "\n".pointer transmute usize, "\n".length];

   let written: usize = 0;
   loop {
      fd_write(1, &data[0], 2, &written);
      let remaining_len = &data[1];
      if written > *remaining_len {
         break;
      } else {
         data[0] = data[0] + written;
         *remaining_len = *remaining_len - written;
      }
   }
}

proc print_bool(value: bool) {
   if value {
      print("true");
   } else {
      print("false");
   }
}

func count_digits(value: u64) -> u8 {
   let digit_count: u8 = 0;

   loop {
      if value <= 9 {
         return digit_count + 1;
      }

      digit_count = digit_count + 1;
      value = value / 10;
   }

   return 0;
}

proc int_to_string(value: u64) -> String {
   let digit_count = count_digits(value);
   let digit_count_for_loop = digit_count extend usize - 1;
   let buf = malloc(digit_count extend usize);

   let next_digit: u8 = 0;
   let i: usize = 0;

   loop {
      if value <= 9 {
         next_digit = value truncate u8;
         let ptr = ((buf transmute usize) + digit_count_for_loop - i) transmute &u8;
         *ptr = next_digit + 48;
         i = i + 1;
         break;
      }
      next_digit = (value % 10) truncate u8;
      value = value / 10;
      let ptr = ((buf transmute usize) + digit_count_for_loop - i) transmute &u8;
      *ptr = next_digit + 48;
      i = i + 1;
   }


   return String {
      length: i,
      pointer: buf,
   };
}

proc malloc(bytes: usize) -> &u8 {
   let num_new_pages = (bytes / 65536) + (bytes % 65536 != 0) extend usize;
   let current_page_start = wasm_memory_grow(num_new_pages) * 65536;
   return current_page_start transmute &u8;
}
